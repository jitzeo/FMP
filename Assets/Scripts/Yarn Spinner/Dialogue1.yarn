title: Start
tags:
---
<<declare $available to false>>
<<declare $anxiety to "none">>
<<declare $fearLevelStart to 0>>
<<declare $differentPerspective to "none">>
<<declare $evidence to "none">>
<<declare $fearLevelEnd to "none">>
<<declare $pressureCount to 0>>
<<declare $selectedOption1 to false>>
<<declare $selectedOption2 to false>>

<<freeze_player Player>>
: In this game you are playing as a person whose village has been tormented by a terrible storm. To stop the ongoing rampage, you have set out on an adventure through the mountainous valley to find the core of the storm to figure out what causes the storm and to drive the it away. 
: Determined you set out on this journey but you remain anxious as you are insecure about your ability to save the village. Along the path you encounter obstacles in the rugged terrain which are analoguous of your anxiety standing in your way.
<<fade FadeOutLeft>>
Player: Okay, where do I need to go now.
Player: Ah I see the flag over there! There's the next waypoint!
Player: Now how do I get there? Let's look for a path.
<<freeze_player Player>>
===

title: Obstacle
tags:
---
<<freeze_player Player>>
Player: Ugh, there's a huge cliff ahead.
Player: How am I going to get over this?
Player: ...
Player: I don't see a clear path.
Player: Let's explore to see if there is a path that leads to the top of this cliff.
<<freeze_player Player>>
===

title: Pressure
tags:
---
<<freeze_player Player>>
<<if $pressureCount == 0>>
    Player: Nope, not through here.
    Mysterious voice: Did you really think <i>you</i> could save the village?
<<elseif $pressureCount == 1>>
    Player: Ugh, this also is a dead end.
    Mysterious voice: You are failing yet again...
<<elseif $pressureCount == 2>>
    Player: This cliff just keeps lurking over me.
    Mysterious voice: You are a failure...
<<elseif $pressureCount >= 3>>
    Player: This cliff feels more and more daunting...
    Mysterious voice: You are no good...
    Player: I am worthless...
    <<input_field "Dialogue System" "What thoughts of you could be your cliff? Which you feel are impossible to get over.">>
    <<set $anxiety to getInputText()>>
    <<input_field "Dialogue System" "How strong is the fear with this thought on a scale of 0-100?">>
    <<set $anxiety to getInputText()>>
    <<jump Transition>>
<<endif>>
<<set $pressureCount = $pressureCount + 1>>
<<freeze_player Player>>
===

title: Transition
tags: 
---
<<fade FadeInLeft>>
<<change_start IslandManager>>
<<rotate_player Player>>
<<fade FadeOutLeft>>
Player: Wha... What happened?!
Player: Everything looks different now.
Player: Aagh, there's a monster!
Player: It's starting to move!
Player: It's coming to get me! I need to find shelter!
<<freeze_player Player>>
<<unfreeze_dino DinoActive>>
<<instructions "Press [<-] or [A] to run away from the monster!" "left">>
===

title: Discovery
tags:
---
<<freeze_player Player>>
<<move_player Player left 0.3>>
<<turn_player Player right>>
<<wait  1>>
<<activate_dino passive>>
Player: Pffff, Hffff...
Player: Deep breaths... Deep breaths... Phew, okay. 
Player: I thought I was going to die!
Player: But what... What was that?
Player: It seemed to have passed out when hitting this cliff.
Player: But why was there a monster all of a sudden?
Player: It seemed to have dropped something... 
<<jump Stick>>
===

title: Stick
---
Player: A stick? What does that mean?
-> It is probably nothing <<if $selectedOption1 == false>>
    Player: Maybe it's nothing.
    Player: A monster with a stick feels odd though.
    <<set $selectedOption1 = true>>
    <<jump Stick>>
-> I can use this to fight the monster! <<if $selectedOption2 == false>>
    Player: I have a weapon now!
    Player: But can it really do any damage to the monster?
    Player: Why did it have a stick in the first place?
    <<set $selectedOption2 = true>>
    <<jump Stick>>
-> What if the monster isn't real?
    Player: There is something about this monster. I need to inspect it further.
    Player: Let me find the courage to get closer to it.
    <<set $selectedOption1 = false>>
    <<set $selectedOption2 = false>>
    <<jump Investigate>>
===

title: Investigate
---
<<spam_button Player "right" 10 "inspect the monster.">>
Player: Aagh, it moved again! 
Player: ... 
Player: No, I think I am okay. I got this!
<<spam_button Player "right" 5 "inspect the monster.">>
Player: It feels like dirt... 
Player: What could this be?
Player: What if I try to look at it from a different perspective?
<<input_field "Dialogue System" "What about your own thoughts? Do you only look at it from one side? What are you leaving out of consideration?">>
<<set $differentPerspective to getInputText()>>
<<tug_interaction "2D Island" true>>
===

title: Investigate2
---
Player: Woah... The landscape changed!
Player: But where did the monster come from?
Player: Let's explore this further.
<<freeze_player Player>>
===

title: Spikes
tags:
---
<<freeze_player Player>>
Player: There are spikes here, I can't get past them.
Player: Where have I seen these sharp spikes before?
// -> Player: Near the cliffside.
// 	Player: Were there really any spikes there?
// -> Player: The monster.
Player: The monster's teeth looked a lot like them!
// -> Player: Could be a deja-vu.
// 	Player: Hmm, could be the case.
Player: How do I get past these spikes though?
Player: Maybe a different perspective might help me find a clue.
<<freeze_player Player>>
<<instructions "Press [Q] or [E] to rotate the camera." "rotate">>
===

title: Tree
tags:
---
<<freeze_player Player>>
Player: There is a tree here.
Player: Could the stick have dropped from here?
<<freeze_player Player>>
===

title: Footprint
tags:
---
<<freeze_player Player>>
Player: Woah!! What a large footprint!
Player: The monster must have passed by here.
Player: Aagh, is it still around?!
Player: I should tread carefully.
<<freeze_player Player>>
===

title: Remarks
tags:
---
Player: Is this where the stick came from?
Player: Could this have been the teeth of the monster?
Player: How do I get past this?
Player: The spikes are gone now!
Player: I am making progress!
===

title: IdentifyThoughts
tags:
---
<<freeze_player Player>>
Player: Where do I go now?
Player: How do I get to the flag?
Player: Let's look at the stuff I found.
Player: The monster felt like dirt...
Player: I found spikes which I saw on the monster as well...
Player: There was a tree from which the stick must have dropped...
Player: But then there was this large footprint as well.
<<jump Identify1>>

<<change_perspective IslandManager>>
//This text is currently not shown to the player
Player: The storm is back.
Player: The monster still seems to be passed out.
Player: But what's making this happen?
//?Option?
Player: My thoughts! The storm is using my thoughts to make me anxious. 
Player: But it all seems so real...
Player: How do I know what to believe?
Player: At least I know how this shift happens. That's a first step to gain control.
Player: But this shifted landscape allows me to reach new areas.
Player: Let's explore further.
<<freeze_player Player>>
===

title: Identify1
tags:
---
Player: What does this say about the monster?
-> The monster is still out there. <<if $selectedOption1 == false>>
    Player: Yeah that could be...
    Player: But where did it go?
    Player: I feel like I am missing something.
    <<set $selectedOption1 = true>>
    <<jump Identify1>>
-> The monster was never there. <<if $selectedOption2 == false>>
    Player: But I saw it with my own eyes.
    Player: It nearly got me!
    Player: There must be something else.
    <<set $selectedOption2 = true>>
    <<jump Identify1>>
-> I am currently standing on the monster.
    Player: Could the monster be these hills from a different perspective?
    <<set $selectedOption1 = false>>
    <<set $selectedOption2 = false>>
    <<change_camera IslandManager>>
    Player: It is! Something made these hills come to life.
    <<change_island IslandManager>>
    <<input_field "Dialogue System" "Are your thoughts true? Are there other explanations that you have not yet thought about?">>
    <<set $evidence to getInputText()>>
    Player: But how could these hills move?
    Player: There is still a lot I don't know.
    Player: But I can move to new places now in this perspective!
    <<freeze_player Player>>
    <<unlock_perspective Player>>
    <<enable_perspective Player>>
    <<instructions "Press [shift] to switch between perspectives." "shift">>
===

title: Challenge
tags:
---
<<freeze_player Player>>
Player: I still don't know how to get to the flag...
Player: How can I get on top of that cliff?
Player: The monster seemed about as tall.
Player: That's it! I might need to use the monster to get there!
Player: But I don't want to face it again...
Player: No, I'll have to face it!
Player: It's just hills and trees.
Player: But how do I get it alive?
<<jump Challenge1>>
===

title: Challenge1
tags:
---
Player: How did it come alive the first time...
-> When I felt hopeless
    <<jump Challenge2>>
-> When I felt confident
    Player: No that doesn't seem right.
    <<jump Challenge1>>
-> When I felt happy
    Player: No that doesn't seem right.
    <<jump Challenge1>>
===

title: Challenge2
tags:
---
Player: That's right! When I felt hopeless, that mysterious voice appeared.
Player: My thoughts made the monster come alive!
Player: I have to go through that feeling again to overcome the monster.
Player: Otherwise I will never reach my goal.
Player: I can't do it.
Mysterious voice: Do <i>you</i> really still think you can save the village?
Player: No, I have come this far.
Mysterious voice: <i>You</i> are failing yet again...
Mysterious voice: <i>You</i> are no good...
Player: I can at least try.
Mysterious voice: <i>You</i> are worthless...

<<change_perspective IslandManager>>
<<enable_perspective Player>>
<<activate_dino end>>

Player: The monster, it.. it's alive again!!
Player: Remember, the monster is just the hills!
Player: Challenge this fear and reach the goal!
Player: Quick, I need to move to its head!
<<freeze_player Player>>
<<activate_stamina Player true>>
===

title: Facing2
tags:
---
Player: This is tougher than I thought...
Player: I need a breather.
Player: I still have to go a long way...
Player: But I have already made it to here.
Player: Let's take it in steps. I will try to reach the back next.
===

title: Facing
tags:
---
<<freeze_player Player>>
Player: HFFF... PFFF...
Player: HFFF... My heart is racing.
Player: PFFF... I am losing control.
Player: HFFF... I need to regain my breath.
Player: PFFF... Gain back control.
<<breathing_minigame true>>
===

title: Facing1
tags:
---
Player: Hfff... Pfff...
Player: That's... That's better.
Player: Alright, I think I can go on again.
Player: Let's get to the head.
<<freeze_player Player>>
===

title: Facing3
tags:
---
Player: I am near my limit.
Player: I just want to give up.
Player: What am I even doing this for?
Player: No, I can't give in now. I am almost there.
Player: Look at how far I already made it.
Player: I can make this last step.
===

title: End
tags:
---
<<activate_stamina Player false>>
<<freeze_player Player>>
<<change_perspective IslandManager>>
Player: I... I did it!
Player: I made it!
Player: My hearts still racing...
Player: But I challenged the monster!
Player: The anxiety is slowly fading.
Player: Deep breaths... Deep breaths...
Player: Pfew... 
Player: Just running from my fears won't help me in the long run.
Player: I need to further learn about this voice if I will be able to control it.
Player: This skill of taking a different perspective will be useful.
Player: I still have much to learn, but I already made it to here.
Player: Let's continue the journey
<<input_field "Dialogue System" "How strong is the fear with the thoughts mentioned at the start now on a scale of 0-100?">>
<<set $fearLevelEnd to getInputText()>>
: Thanks for playing this demo!
<<freeze_player Player>>
===
